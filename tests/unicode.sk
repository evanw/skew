namespace Skew.Tests {
  def toString(list List<int>) string {
    return "[" + ", ".join(list.map<string>(x => x.toString)) + "]"
  }

  def testUnicode(text string, codePoints List<int>) {
    testSimple("codePoints " + text, expectString => expectString(toString(codePoints), toString(text.codePoints)))
    testSimple("fromCodePoints " + text, expectString => expectString(text, string.fromCodePoints(codePoints)))
    testSimple("codeUnits " + text, expectString => expectString(text, string.fromCodeUnits(text.codeUnits)))
  }

  def testUnicode {
    testUnicode("Цлїςσδε", [1062, 1083, 1111, 962, 963, 948, 949])
    testUnicode("ﾌﾑｱﾑｶﾓｹﾓ", [65420, 65425, 65393, 65425, 65398, 65427, 65401, 65427])
    testUnicode("フムヤムカモケモ", [12501, 12512, 12516, 12512, 12459, 12514, 12465, 12514])
    testUnicode("㊀㊁㊂㊃㊄", [12928, 12929, 12930, 12931, 12932])
    testUnicode("☳☶☲", [9779, 9782, 9778])
  }
}

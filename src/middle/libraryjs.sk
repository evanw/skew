namespace Skew {
  const NATIVE_LIBRARY_JS = "
const __create fn(dynamic) dynamic = dynamic.Object.create ? dynamic.Object.create : prototype => {
  return {\"__proto__\": prototype}
}

const __extends = (derived dynamic, base dynamic) => {
  derived.prototype = __create(base.prototype)
  derived.prototype.constructor = derived
}

const __imul fn(int, int) int = dynamic.Math.imul ? dynamic.Math.imul : (a, b) => {
  return ((a as dynamic) * (b >>> 16) << 16) + (a as dynamic) * (b & 65535) | 0
}

const __prototype dynamic
const __isInt = (value dynamic) => value == (value | 0)
const __isBool = (value dynamic) => value == !!value
const __isDouble = (value dynamic) => dynamic.typeof(value) == \"number\"
const __isString = (value dynamic) => dynamic.typeof(value) == \"string\"
const __asString = (value dynamic) => value == null ? value : value + \"\"

def assert(truth bool) {
  if !truth {
    throw dynamic.Error(\"Assertion failed\")
  }
}

@import
namespace Math {}

@rename(\"boolean\")
class bool {}

@rename(\"number\")
class int {}

@rename(\"number\")
class double {
  def isFinite bool {
    return dynamic.isFinite(self)
  }

  def isNaN bool {
    return dynamic.isNaN(self)
  }
}

class string {
  def <=>(x string) int {
    return ((x as dynamic < self) as int) - ((x as dynamic > self) as int)
  }

  def slice(start int) string {
    assert(0 <= start && start <= count)
    return (self as dynamic).slice(start)
  }

  def slice(start int, end int) string {
    assert(0 <= start && start <= end && end <= count)
    return (self as dynamic).slice(start, end)
  }

  def startsWith(text string) bool {
    return count >= text.count && slice(0, text.count) == text
  }

  def endsWith(text string) bool {
    return count >= text.count && slice(count - text.count) == text
  }

  def replaceAll(before string, after string) string {
    return after.join(self.split(before))
  }

  def in(value string) bool {
    return indexOf(value) != -1
  }

  def count int {
    return (self as dynamic).length
  }

  def [](index int) int {
    assert(0 <= index && index < count)
    return (self as dynamic).charCodeAt(index)
  }

  def get(index int) string {
    assert(0 <= index && index < count)
    return (self as dynamic)[index]
  }

  def repeat(times int) string {
    var result = \"\"
    for i in 0..times {
      result += self
    }
    return result
  }

  def join(parts List<string>) string {
    return (parts as dynamic).join(self)
  }

  def codeUnits List<int> {
    var result List<int> = []
    for i in 0..count {
      result.append(self[i])
    }
    return result
  }
}

namespace string {
  def fromCodeUnit(codeUnit int) string {
    return dynamic.String.fromCharCode(codeUnit)
  }

  def fromCodeUnits(codeUnits List<int>) string {
    var result = \"\"
    for codeUnit in codeUnits {
      result += string.fromCodeUnit(codeUnit)
    }
    return result
  }
}

class StringBuilder {
  var buffer = \"\"

  def new {
  }

  def append(x string) {
    buffer += x
  }

  def toString string {
    return buffer
  }
}

@rename(\"Array\")
class List {
  @rename(\"unshift\")
  def prepend(x T)

  @rename(\"push\")
  def append(x T)

  @rename(\"every\")
  def all(x fn(T) bool) bool

  @rename(\"some\")
  def any(x fn(T) bool) bool

  @rename(\"slice\")
  def clone List<T>

  @rename(\"forEach\")
  def each(x fn(T))

  def slice(start int) List<T> {
    assert(0 <= start && start <= count)
    return (self as dynamic).slice(start)
  }

  def slice(start int, end int) List<T> {
    assert(0 <= start && start <= end && end <= count)
    return (self as dynamic).slice(start, end)
  }

  def [](index int) T {
    assert(0 <= index && index < count)
    return (self as dynamic)[index]
  }

  def []=(index int, value T) T {
    assert(0 <= index && index < count)
    return (self as dynamic)[index] = value
  }

  def in(value T) bool {
    return indexOf(value) != -1
  }

  def isEmpty bool {
    return count == 0
  }

  def count int {
    return (self as dynamic).length
  }

  def first T {
    assert(!isEmpty)
    return self[0]
  }

  def last T {
    assert(!isEmpty)
    return self[count - 1]
  }

  def prepend(values List<T>) {
    assert(values != self)
    var count = values.count
    for i in 0..count {
      prepend(values[count - i - 1])
    }
  }

  def append(values List<T>) {
    assert(values != self)
    for value in values {
      append(value)
    }
  }

  def insert(index int, values List<T>) {
    assert(values != self)
    for value in values {
      insert(index, value)
      index++
    }
  }

  def insert(index int, value T) {
    assert(0 <= index && index <= count)
    (self as dynamic).splice(index, 0, value)
  }

  def removeFirst {
    assert(!isEmpty)
    (self as dynamic).shift()
  }

  def takeFirst T {
    assert(!isEmpty)
    return (self as dynamic).shift()
  }

  def removeLast {
    assert(!isEmpty)
    (self as dynamic).pop()
  }

  def takeLast T {
    assert(!isEmpty)
    return (self as dynamic).pop()
  }

  def removeAt(index int) {
    assert(0 <= index && index < count)
    (self as dynamic).splice(index, 1)
  }

  def takeAt(index int) T {
    assert(0 <= index && index < count)
    return (self as dynamic).splice(index, 1)[0]
  }

  def takeRange(start int, end int) List<T> {
    assert(0 <= start && start <= end && end <= count)
    return (self as dynamic).splice(start, end - start)
  }

  def appendOne(value T) {
    if !(value in self) {
      append(value)
    }
  }

  def removeOne(value T) {
    var index = indexOf(value)
    if index >= 0 {
      removeAt(index)
    }
  }

  def removeRange(start int, end int) {
    assert(0 <= start && start <= end && end <= count)
    (self as dynamic).splice(start, end - start)
  }

  def removeIf(callback fn(T) bool) {
    var index = 0

    # Remove elements in place
    for i in 0..count {
      if !callback(self[i]) {
        if index < i {
          self[index] = self[i]
        }
        index++
      }
    }

    # Shrink the array to the correct size
    while index < count {
      removeLast
    }
  }

  def equals(other List<T>) bool {
    if count != other.count {
      return false
    }
    for i in 0..count {
      if self[i] != other[i] {
        return false
      }
    }
    return true
  }
}

namespace List {
  def new List<T> {
    return [] as dynamic
  }
}

namespace StringMap {
  def new StringMap<T> {
    return dynamic.Map.new
  }
}

class StringMap {
  def [](key string) T {
    assert(key in self)
    return (self as dynamic).get(key)
  }

  def []=(key string, value T) T {
    (self as dynamic).set(key, value)
    return value
  }

  def {...}(key string, value T) StringMap<T> {
    self[key] = value
    return self
  }

  def in(key string) bool {
    return (self as dynamic).has(key)
  }

  def count int {
    return (self as dynamic).size
  }

  def isEmpty bool {
    return count == 0
  }

  def get(key string, defaultValue T) T {
    const value = (self as dynamic).get(key)
    return value != dynamic.void(0) ? value : defaultValue # Compare against undefined so the key is only hashed once for speed
  }

  def keys List<string> {
    return dynamic.Array.from((self as dynamic).keys())
  }

  def values List<T> {
    return dynamic.Array.from((self as dynamic).values())
  }

  def clone StringMap<T> {
    return dynamic.Map.new(self)
  }

  def remove(key string) {
    (self as dynamic).delete(key)
  }

  def each(x fn(string, T)) {
    (self as dynamic).forEach((value, key) => {
      x(key, value)
    })
  }
}

namespace IntMap {
  def new IntMap<T> {
    return dynamic.Map.new
  }
}

class IntMap {
  def [](key int) T {
    assert(key in self)
    return (self as dynamic).get(key)
  }

  def []=(key int, value T) T {
    (self as dynamic).set(key, value)
    return value
  }

  def {...}(key int, value T) IntMap<T> {
    self[key] = value
    return self
  }

  def in(key int) bool {
    return (self as dynamic).has(key)
  }

  def count int {
    return (self as dynamic).size
  }

  def isEmpty bool {
    return count == 0
  }

  def get(key int, defaultValue T) T {
    const value = (self as dynamic).get(key)
    return value != dynamic.void(0) ? value : defaultValue # Compare against undefined so the key is only hashed once for speed
  }

  def keys List<int> {
    return dynamic.Array.from((self as dynamic).keys())
  }

  def values List<T> {
    return dynamic.Array.from((self as dynamic).values())
  }

  def clone IntMap<T> {
    return dynamic.Map.new(self)
  }

  def remove(key int) {
    (self as dynamic).delete(key)
  }

  def each(x fn(int, T)) {
    (self as dynamic).forEach((value, key) => {
      x(key, value)
    })
  }
}
"
}
